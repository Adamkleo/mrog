<program>           ::= <statement>*

<statement>         ::= <function_definition>
                      | <built_in_function>

<built_in_function> ::= PRINT "(" <expression> ")"



<function_definition> ::= ID "(" ID ")" "=" <expression>

<expression>        ::= <term> <rest_expression>

<rest_expression>   ::= "+" <term> <rest_expression>
                      | "-" <term> <rest_expression>
                      | ε

<term>              ::= <factor> <rest_term>

<rest_term>         ::= "*" <factor> <rest_term>
                      | "/" <factor> <rest_term>
                      | ε

<factor>            ::= <primary> <rest_factor>

<rest_factor>       ::= "^" <primary> <rest_factor>
                      | ε

<primary>           ::= <math_function> "(" <expression> ")"
                      | <number> <postfix_operator>
                      | <variable> <postfix_operator>
                      | "(" <expression> ")" <postfix_operator>
                      | <identifier>

<identifier>        ::= ID "(" <expression> ")"
                      | ID "'" "(" <expression> ")" 
                      | ID

<postfix_operator>  ::= "!" <postfix_operator>
                      | ε

<math_function>     ::= TRIG_FUNCTION
                      | LN
                      | EXP
                      | ABS

<number>            ::= [0-9]+

<variable>          ::= x | y | z
